/*
This table queries the model to make predictions over the dataset.
*/

config {
  type: "table",
  description: "This table contains the datamart with the predicted values",
  columns: {
    AreaCode: "The identifier of a parking zone with city_code-number_with_0_padding, representing the specific parking area.",
    TimeStart1H: "The datetime of the data, truncated to the start of each hour, used for temporal analysis.",
    Capacity: "The maximum number of parking slots that the area has, defining the potential supply.",
    PredictedValue: "Extracted hour from TimeStart1H, used for hourly trend analysis.",
    AvailableSpots: "Parking spots available.",
  }
}

SELECT
  AreaCode,
  predicted_label as PredictedValue,
  TimeStart1H,
  Capacity,
  LEAST(ROUND(GREATEST(predicted_label, 0), 0), Capacity) as AvailableSpots,
FROM
  ML.PREDICT(MODEL `${dataform.projectConfig.defaultSchema}.${constants.MODEL}`,
    (SELECT
      Capacity,
      AreaCode,
      TimeStart1H,
      AverageTicketsPastWeek,
      AverageTicketsPerDayHour,
      AverageTicketsPerHour,
      STDTicketsPerDayHour,
      is_mon,
      is_tue,
      is_wed,
      is_thu,
      is_fri,
      is_sat,
      EXTRACT(HOUR from TimeStart1H) as Hour,
      FROM
        ${ref('datamart_inference')}
    )
  )
